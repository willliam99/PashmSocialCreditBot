import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    kotlin("jvm") version "2.2.21"
    application
    alias(libs.plugins.kotlin.serialization)
}

group = "com.xeniac"
version = "2.1.5"

kotlin {
    compilerOptions {
        jvmToolchain(jdkVersion = 23)
        jvmTarget = JvmTarget.fromTarget(target = "23")

        // Enable Context-Sensitive Resolution in Kotlin 2.2
        freeCompilerArgs.add("-Xcontext-sensitive-resolution")
    }
}

dependencies {
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.kotlinx.datetime)

    // Kotlin Telegram Bot Library
    implementation(libs.bundles.telegram)

    // Ktor Client Library
    implementation(libs.bundles.ktor)

    // Exposed SQL library
    implementation(libs.bundles.exposed)

    // PostgreSQL Driver Library
    implementation(libs.postgresql)

    // Logback Classic Library for SLF4J
    implementation(libs.logback.classic)
}

application {
    mainClass = "MainKt"
}

tasks.jar {
    manifest {
        attributes["Main-Class"] = "MainKt"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    configurations["compileClasspath"].forEach { file: File ->
        from(zipTree(file.absoluteFile))
    }
}